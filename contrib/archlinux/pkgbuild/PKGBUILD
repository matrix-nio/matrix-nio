# $Id$
# Maintainer: Damir JeliÄ‡ <poljar@termina.org.uk>

pkgbase=python-nio
_pypiname=matrix-nio
pkgname=('python-nio' 'python2-nio')
pkgver=0.4.1
pkgrel=1
pkgdesc='python no-IO library for the matrix chat protocol'
arch=('any')
url='https://github.com/poljar/matrix-nio'
license=('ISC')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=()
source=("https://github.com/poljar/matrix-nio/archive/$pkgver.tar.gz")
sha512sums=('bab90911d95a1551df2ec1e15ced0ca020317b9d6329934fa6016017852c773ef47bd5c075e240f4230a39b788c8d329635507ec145e97becc858319d1ec8224')

prepare() {
  cd "$srcdir"
  cp -a $_pypiname-$pkgver{,-py2}
}

package_python-nio() {
  depends=('python' 'python-olm' 'python-h11' 'python-h2'
           'python-jsonschema' 'python-logbook' 'python-attrs'
           'python-peewee' 'python-atomicwrites' 'python-future'
           'python-pycryptodome' 'python-unpaddedbase64')
  cd "$srcdir"/$_pypiname-$pkgver

  python setup.py install --root="${pkgdir}/" --optimize=1
}

package_python2-nio() {
  depends=('python2' 'python2-olm' 'python2-h11' 'python2-h2'
           'python2-jsonschema' 'python2-logbook' 'python2-attrs'
           'python2-peewee' 'python2-atomicwrites' 'python2-typing'
           'python2-future' 'python2-pycryptodome' 'python2-unpaddedbase64')
  cd "$srcdir"/$_pypiname-$pkgver-py2

  python2 setup.py install --root="${pkgdir}/" --optimize=1
}
